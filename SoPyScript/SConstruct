###
# Copyright (c) 2002, Tamer Fahmy <tamer@tammura.at>
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#   * Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer.
#   * Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in
#     the documentation and/or other materials provided with the
#     distribution.
#   * Neither the name of the copyright holder nor the names of its
#     contributors may be used to endorse or promote products derived
#     from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#

import os, sys
import distutils.sysconfig

SetOption('implicit_cache', 1)

opts = Options('custom.py')
opts.AddOptions(BoolOption('warnings', 'compilation with -Wall', 1),
                EnumOption('debug', 'debug output and symbols', 'no',
                           allowed_values=('yes', 'no', 'full'),
                           map={}, ignorecase=0))

env = Environment(ENV = os.environ,
                  options = opts,
                  tools=['default'])

Help(opts.GenerateHelpText(env))

if env['debug'] in ('yes', 'full'):
    if sys.platform == "win32":
        env.Append(CCFLAGS = ['/Zi'])
    else:
        env.Append(CCFLAGS = ['-g'])
else:
    if sys.platform == "win32":
        env.Append(CCFLAGS = '/O2')
    else:
        env.Append(CCFLAGS = '-O2')

if not sys.platform == "win32" and  env['warnings']:
    env.Append(CCFLAGS = '-Wall')

sopyscript_env = env.Copy(CPPPATH=".")
sopyscript = sopyscript_env.Object(Split('SoPyScript.cpp'),
                                   CPPPATH=['.', distutils.sysconfig.get_python_inc()])

examin_env = sopyscript_env.Copy()
examin_env.ParseConfig("soqt-config --cppflags --libs --ldflags")

examin_env.Append(LIBPATH = distutils.sysconfig.get_python_lib())
examin_env.Append(LIBS = 'pivy_runtime')
examin_env.Append(LINKFLAGS = distutils.sysconfig.get_config_vars()['BLDLIBRARY'].split())
if sys.platform != 'win32':
    examin_env.Append(LINKFLAGS = '-Wl,-R' + distutils.sysconfig.get_python_lib())

examin_env.Program('examin', Split('examin.cpp') + sopyscript)
